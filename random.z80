; http://www.retroprogramming.com/2017/07/xorshift-pseudorandom-numbers-in-z80.html
; 16-bit xorshift pseudorandom number generator
; 20 bytes, 86 cycles (excluding ret)

random:
    ; modifies: hl, a
    ; on exit, HL = 16-bit pseudorandom number
    ;           A = top 8-bits (i.e. H)
    seed: equ $+1
    ld hl, 0xa64d ; seed must not be 0
    ld a, h
    rra
    ld a, l
    rra
    xor h
    ld h, a
    ld a, l
    rra
    ld a, h
    rra
    xor l
    ld l, a
    xor h
    ld h, a
    ld (seed),hl
    ; a is still top 8 bits ()
    ret
