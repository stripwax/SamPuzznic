render_puzzle_tiles:
                ld hl, tiles ; where the tile data is
                ld bc, 10*256+12 ; count 12 rows and 10 columns
                ld de, 32 ; this is 12*0+0+32 i.e. x=0 and y=0
    @next_col:
                ld a, (hl)
                push bc ; TODO OPTIMIZE THIS AWAY
                push hl
                call draw_tile_at_de_tile_a
                pop hl
                res 7, e ; because, draw_tile_at_de_tile_a ends with DE one pixel row below above start pixel
                inc d
                pop bc ; TODO OPTIMIZE THIS AWAY
                ld a, e
                add 8 ; 16 pixels to the right
                ld e, a
                inc hl
                djnz @-next_col
                ; now, skip the next 6 bytes of hl for the next row
                inc hl
                inc hl
                inc hl
                inc hl
                inc hl
                inc hl
                ld b, 10 ; reset column counters for next row
                ld e, 32 ; column zero
                ld a, d ; next row on screen (128*16 pixels = 256*8 bytes)
                add 8   ; ...
                ld d, a ; ...
                dec c
                jr nz, @-next_col
                ret

count_remaining_tiles:
                ; TODO inline with render_puzzle_tiles, since it's one-off at start of puzzle
                ret

render_remaining_tiles:
                ; this draws the bitmaps AND the counts.
                ; but ideally it only draws what it needs to (don't draw bitmaps if counts
                ; still > 0)
                ; and maybe we can stagger the updates over a few frames
                ; 
                ret

debug_memcpy_level_data:
                ld a, 20+2 ; level 3-3 (zero-based level so 0 is level 1, etc)
                jp memcpy_level_data

memcpy_level_data:
                ; on entry, a = which level
                add a, a ; pointers are two bytes, so need offset*2 into the index
                ld hl, PUZZLEDATA_INDEX
                add a, l
                ld l, a
                jr nc, @ok
                inc h ; carry
        @ok:
                ; To save space, the levels are (slightly) compressed
                ; Our grid in game state is 16x12 but our level data is just 10x12
                ; so just copy 10 bytes, 12 times
                ; TODO more compression but only if necessary
                ld a, (hl)
                inc hl
                ld h, (hl)
                ld l, a
                ld de, tiles
                ld a, 12 ; count number of rows
            @next:
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                inc de
                inc de
                inc de
                inc de
                inc de
                inc de
                dec a
                jp nz, @-next
                ; finally copy the remaining pieces
                ld bc, puzzle_copy_len - 10*12
                ldir
                ret


init_game_state:
                ; call after doing memcpy_level_data
                ; basically a memclear but also a few other things
                ld hl, game_state_zero_start
                xor a
                ld (hl), a
                ld de, game_state_zero_start + 1
                ld bc, game_state_zero_end - game_state_zero_start - 1
                LDIR

                ld hl, tiles_flags
                xor a
                ld (hl), a
                ld de, tiles_flags+1
                ld bc, 16*12-1
                LDIR

                call init_cursor_controller

                ret
